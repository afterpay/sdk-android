buildscript {
    ext.versions = [
        'compile_sdk'               : 31,
        'min_sdk'                   : 24,
        'java'                      : '1.8',
        'build_tools'               : '30.0.3',
        'android_gradle_plugin'     : '7.0.4',
        'kotlin'                    : '1.6.0',
        'kotlinx_coroutines'        : '1.5.1',
        'kotlinx_serialization'     : '1.3.1',
        'maven_publish_plugin'      : '0.17.0',
        'androidx_activity'         : '1.2.3',
        'androidx_constraint_layout': '2.1.2',
        'androidx_fragment'         : '1.4.0',
        'androidx_lifecycle'        : '2.4.0',
        'androidx_navigation'       : '2.3.5',
        'androidx_recycler_view'    : '1.2.1',
        'android_three_ten'         : '1.3.1',
        'app_compat'                : '1.3.0',
        'core_ktx'                  : '1.6.0',
        'material_design_components': '1.4.0',
        'moshi'                     : '1.12.0',
        'okhttp'                    : '4.9.1',
        'retrofit'                  : '2.9.0',
        'junit'                     : '4.13.2',
        'androidx_test_orchestrator': '1.4.0',
        'androidx_test_core'        : '1.4.0',
        'androidx_test_junit'       : '1.1.3',
        'androidx_test_runner'      : '1.4.0',
        'espresso'                  : '3.4.0',
        'uiautomator'               : '2.2.0',
        'ktlint'                    : '0.43.2'
    ]

    repositories {
        mavenCentral()
        google()
    }

    dependencies {
        classpath "com.android.tools.build:gradle:${versions.android_gradle_plugin}"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:${versions.kotlin}"
        classpath "org.jetbrains.kotlin:kotlin-serialization:${versions.kotlin}"
        classpath "com.vanniktech:gradle-maven-publish-plugin:${versions.maven_publish_plugin}"
    }
}

subprojects {
    repositories {
        mavenCentral()
        google()
    }

    group = GROUP
    version = VERSION_NAME

    configurations {
        ktlint
    }

    dependencies {
        ktlint "com.pinterest:ktlint:${versions.ktlint}"
    }

    task ktlint(type: JavaExec, group: "verification") {
        description = "Check Kotlin code style."
        classpath = configurations.ktlint
        main = "com.pinterest.ktlint.Main"
        args "src/**/*.kt"
    }

    task ktlintFormat(type: JavaExec, group: "formatting") {
        description = "Fix Kotlin code style deviations."
        classpath = configurations.ktlint
        main = "com.pinterest.ktlint.Main"
        args "-F", "src/**/*.kt"
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}
