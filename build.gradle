buildscript {
    ext.versions = [
        'compile_sdk': 30,
        'min_sdk': 21,
        'java': '1.8',
        'build_tools': '30.0.3',
        'android_gradle_plugin': '4.1.2',
        'kotlin': '1.4.32',
        'kotlinx_coroutines': '1.4.2',
        'kotlinx_serialization': '1.1.0',
        'maven_publish_plugin': '0.12.0',
        'androidx_activity': '1.2.2',
        'androidx_constraint_layout': '2.0.4',
        'androidx_fragment': '1.3.2',
        'androidx_lifecycle': '2.3.1',
        'androidx_navigation': '2.3.5',
        'androidx_recycler_view': '1.2.0',
        'android_three_ten': '1.2.4',
        'app_compat': '1.2.0',
        'core_ktx': '1.3.2',
        'material_design_components': '1.3.0',
        'moshi': '1.11.0',
        'okhttp': '4.9.0',
        'retrofit': '2.9.0',
        'junit': '4.13.2',
        'ktlint': '0.37.2'
    ]

    repositories {
        mavenCentral()
        google()
        jcenter()
    }

    dependencies {
        classpath "com.android.tools.build:gradle:${versions.android_gradle_plugin}"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:${versions.kotlin}"
        classpath "org.jetbrains.kotlin:kotlin-serialization:${versions.kotlin}"
        classpath "com.vanniktech:gradle-maven-publish-plugin:${versions.maven_publish_plugin}"
    }
}

subprojects {
    repositories {
        mavenCentral()
        google()
        jcenter()
    }

    group = GROUP
    version = VERSION_NAME

    configurations {
        ktlint
    }

    dependencies {
        ktlint "com.pinterest:ktlint:${versions.ktlint}"
    }

    task ktlint(type: JavaExec, group: "verification") {
        description = "Check Kotlin code style."
        classpath = configurations.ktlint
        main = "com.pinterest.ktlint.Main"
        args "src/**/*.kt"
    }

    task ktlintFormat(type: JavaExec, group: "formatting") {
        description = "Fix Kotlin code style deviations."
        classpath = configurations.ktlint
        main = "com.pinterest.ktlint.Main"
        args "-F", "src/**/*.kt"
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}
